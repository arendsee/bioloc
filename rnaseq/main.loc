@comment

This RNA-seq example is based off the EBI tutorial at:
https://www.ebi.ac.uk/training/online/course/ebi-next-generation-sequencing-practical-course/rna-sequencing/rna-seq-analysis-transcriptome

@path
main ::
  null
  .
  cuffdiff
  .
  cuffmerge
  .
  *map_cufflinks id
  .
  *map_tophat

map_cufflinks ::
    map . &(cufflinks . $1) id

map_tophat ::
    map . &( tophat . <bowtie2-build> (lhs . $1) (rhs . $1) )
           ( c . 'leaf' 'flower' )

_ :: bowtie2-build . get_reference

@cache
bowtie2-build :: datcache

@2
tophat := get_fastq . $1
tophat :+ symlink . <get_fastq> $1

@3
tophat := rm . <get_fastq>

@assert
tophat :+ is_fastq . <lhs>
tophat :+ is_fastq . <rhs>
tophat :+ is_in_sequence_space . <lhs>
tophat :+ is_in_sequence_space . <rhs>
tophat :+ equal_length . <lhs> <rhs>


@arg
# arguments shared between all programs
tophat,cufflinks,cuffdiff :: --library-type fr-unstranded

tophat    :: --solexa-quals -g 2 -p 8
cufflinks :: -p 8 -u
cuffdiff  :: -T -u

@include
accessors
validators
wrappers
